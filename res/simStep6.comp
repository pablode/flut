#extension GL_ARB_bindless_texture: require

layout (local_size_x = 64) in;

layout(location = 0, r32ui, bindless_image) uniform restrict readonly uimage3D grid;
layout(location = 1, bindless_sampler) uniform sampler3D velocity;
layout(location = 2) uniform float DT;
layout(location = 3) uniform vec3 GRAVITY;

struct Particle
{
  vec3 position;
  float density;
  vec3 velocity;
  float pressure;
};

layout(binding = 0, std430) restrict buffer particleBuf
{
  Particle particles[];
};

const ivec3 NEIGHBORHOOD_LUT[27] = {
  ivec3(-1, -1, -1), ivec3(0, -1, -1), ivec3(1, -1, -1),
  ivec3(-1, -1,  0), ivec3(0, -1,  0), ivec3(1, -1,  0),
  ivec3(-1, -1,  1), ivec3(0, -1,  1), ivec3(1, -1,  1),
  ivec3(-1,  0, -1), ivec3(0,  0, -1), ivec3(1,  0, -1),
  ivec3(-1,  0,  0), ivec3(0,  0,  0), ivec3(1,  0,  0),
  ivec3(-1,  0,  1), ivec3(0,  0,  1), ivec3(1,  0,  1),
  ivec3(-1,  1, -1), ivec3(0,  1, -1), ivec3(1,  1, -1),
  ivec3(-1,  1,  0), ivec3(0,  1,  0), ivec3(1,  1,  0),
  ivec3(-1,  1,  1), ivec3(0,  1,  1), ivec3(1,  1,  1)
};

void main()
{
  const uint particleId = gl_GlobalInvocationID.x;

  if (particleId >= PARTICLE_COUNT)
  {
    return;
  }

  const Particle particle = particles[particleId];

  const ivec3 voxelId = ivec3(INV_CELL_SIZE * (particle.position - GRID_ORIGIN));

  vec3 forcePressure = vec3(0.0);
  vec3 forceViscosity = vec3(0.0);

  #pragma unroll 1
  for (uint i = 0; i < 27; ++i)
  {
    const ivec3 newVoxelId = voxelId + NEIGHBORHOOD_LUT[i];

    if (any(lessThan(newVoxelId, ivec3(0))) ||
        any(greaterThanEqual(newVoxelId, GRID_RES)))
    {
      continue;
    }

    const uint voxelValue = imageLoad(grid, newVoxelId).r;

    const uint voxelParticleOffset = (voxelValue >> 8);
    const uint voxelParticleCount = (voxelValue & 0xFF);

    for (uint p = 0; p < voxelParticleCount; ++p)
    {
      const uint otherParticleId = voxelParticleOffset + p;

      const Particle otherParticle = particles[otherParticleId];

      const vec3 r = particle.position - otherParticle.position;

      const float rLen = length(r);

      if (rLen >= KERNEL_RADIUS)
      {
        continue;
      }

      vec3 weightPressure = vec3(0.0);

      if (rLen > 0.0)
      {
        weightPressure = PRESS_KERNEL_WEIGHT_CONST * pow(KERNEL_RADIUS - rLen, 3) * (r / rLen);
      }

      const float pressure = particle.pressure + otherParticle.pressure;

      forcePressure += (MASS * pressure * weightPressure) / (2.0 * otherParticle.density);

      const float weightVis = VIS_KERNEL_WEIGHT_CONST * (KERNEL_RADIUS - rLen);

      const vec3 filteredVelocity = texture(velocity, (otherParticle.position - GRID_ORIGIN) / GRID_SIZE).xyz;

      const vec3 velocityDiff = filteredVelocity - particle.velocity;

      forceViscosity += (MASS * velocityDiff * weightVis) / otherParticle.density;
    }
  }

  const vec3 forceGravity = GRAVITY * particle.density;

  const vec3 force = (forceViscosity * VIS_COEFF) - forcePressure + forceGravity;

  const vec3 acceleration = force / particle.density;

  particles[particleId].velocity += acceleration * DT;
}
